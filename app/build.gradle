apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.github.ben-manes.versions'
apply from: "../gradle/base.gradle"

def apiVersion = "1.1"
def minorVersion = "4"
def versionString = apiVersion + "." + minorVersion
def buildCode = getBuildCode()
def buildName = versionString + "." + buildCode.toString() + "-" + getGitDescribe()
def packageName = "at.roteskreuz.stopcorona"

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId packageName
        minSdkVersion 21
        targetSdkVersion 29
        versionCode buildCode
        versionName buildName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                nearbyApiKey: "insert_google_nearby_api_key",
        ]
    }

    signingConfigs {
        release {
            keyAlias getKeyAlias()
            storeFile file("../keystore/release.keystore")
            storePassword getStorePassword()
            keyPassword getKeyPassword()
        }

        debug {
            keyAlias "android_debug_key"
            storeFile file("../keystore/android_debug.keystore")
            storePassword "android_debug"
            keyPassword "android_debug"
        }
    }

    flavorDimensions "default"

    productFlavors {
        stageApi {
            applicationIdSuffix ".stage"
            versionName versionString + "." + buildCode.toString() + "-" + getGitDescribe() + "-stageApi"
        }

        prodApi {
            // Add PlayStore specific configurations
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            // don't generate crashlytics id on each build https://developer.android.com/studio/build/optimize-your-build?utm_source=android-studio#disable-crashlytics-build-id
            ext.alwaysUpdateBuildId = false
        }
        release {
            minifyEnabled true
            // Commented out because it breaks CI. Do not put back in!
            // In release builds, the signing config is provided by the CI
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

dependencies {

    implementation project(":core")

    kapt "androidx.room:room-compiler:2.2.5"

    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.cardview:cardview:1.0.0"

    implementation "android.arch.lifecycle:extensions:1.1.1"

    // Firebase messaging
    implementation 'com.google.firebase:firebase-messaging:20.1.6'

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    kapt "com.airbnb.android:epoxy-processor:3.9.0"

    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.9.2"

    debugImplementation "com.facebook.stetho:stetho:1.5.1"

    def glideVersion = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    def nearbyVersion = "17.0.0"
    implementation "com.google.android.gms:play-services-nearby:$nearbyVersion"

    def spongyCastleCoreVersion = "1.58.0.0"
    implementation "com.madgag.spongycastle:core:$spongyCastleCoreVersion"
    implementation "com.madgag.spongycastle:prov:$spongyCastleCoreVersion"
    implementation "com.madgag.spongycastle:bcpkix-jdk15on:$spongyCastleCoreVersion"

    def work_version = "2.3.4"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-rxjava2:$work_version"

    implementation 'ch.uepaa.p2p:p2pkit-android:2.1.3'

    implementation "com.airbnb.android:lottie:3.4.0"

    testImplementation "junit:junit:4.13"
    testImplementation "org.threeten:threetenbp:1.3.8"
}

// Used in the .gitlab-ci.yml to access the app version
task getAppVersion {
    println " version = $versionString"
}
